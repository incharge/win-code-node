@rem Create a new bash shell environment in the current folder using the git-for-windows bundled bash
@rem If the git installer is in the current folder, or the "installer" subfolder then it will be used, otherwise it will be downloaded.
@rem This is to avoid repeated large downloads when developing, testing and troubleshooting this batch file.
@echo off

setlocal

rem For available git-for-windows versions, see https://github.com/git-for-windows/git/releases/
set GITVERSION=2.30.1
rem set GITVERSION=2.43.0
set GITINSTALLERFILE=PortableGit-%GITVERSION%-64-bit.7z.exe

if exist "bin\git" (
	rem git-for-windows is already installed, so skip the download & installation
	goto gitisinstalled
)

if exist "%GITINSTALLERFILE%" (
	rem Use the git installer in the current folder
	set GITINSTALLERPATH=%GITINSTALLERFILE%
	echo Using existing git installer: %GITINSTALLERPATH%
	goto havegitinstaller
)

set GITINSTALLERPATH=installers\%GITINSTALLERFILE%
if exist "%GITINSTALLERPATH%" (
	echo Using existing git installer: %GITINSTALLERPATH%
	goto havegitinstaller
)

if exist "installers" (
	echo Using existing "installers" folder
) else (
	echo Creating folder "installers"
	mkdir "installers"
)
if not exist "installers" (
	echo Failed to create folder: "installers"
	exit /b
)

echo Downloading the git installer: %GITINSTALLERFILE%
curl.exe --fail --location --output "%GITINSTALLERPATH%" --url "https://github.com/git-for-windows/git/releases/download/v%GITVERSION%.windows.1/%GITINSTALLERFILE%"
if not exist "%GITINSTALLERPATH%" (
	echo Failed to download git
	exit /b
)
echo Successfully downloaded git installer
:havegitinstaller

echo Installing git
"%GITINSTALLERPATH%" -y "-obin\git"
:gitisinstalled

rem Create .bashrc
if not exist .bashrc (
	echo # generated by win-code-node>> .bashrc
	echo # This runs every time a bash shell starts that is not a login>> .bashrc
	echo # If this is a nested bash shell then exit, because settings are inherited,>> .bashrc
	echo # so there's no need to run .bashrc again>> .bashrc
	echo if [[ $SHLVL -ne 1 ]]; then>> .bashrc
	rem echo 	echo Skipping ~/.bashrc in a nested shell>> .bashrc
	echo 	return>> .bashrc
	echo fi>> .bashrc
	rem echo echo Running ~/.bashrc in a top-level shell>> .bashrc
)

rem Create .bash_profile
if not exist .bash_profile (
	echo # generated by win-code-node>> .bash_profile
	rem echo echo Running ~/.bash_profile>> .bash_profile
	echo test -f ~/.profile ^&^& . ~/.profile>> .bash_profile
	echo test -f ~/.bashrc ^&^& . ~/.bashrc>> .bash_profile
)

if exist win-code-node (
	echo Using existing clone of win-code-node
) else (
	bin\git\bin\git clone https://github.com/incharge/win-code-node.git
)

rem Configure bash shell
set HOME=%cd%
set BASH_ENV=%HOME%/.bashrc

rem Run a bash shell to install nvm
bin\git\bin\bash -c "source win-code-node/install-nvm.sh; exit"

rem Run a bash shell to install node and VS Code
bin\git\bin\bash -c "source win-code-node/install-code-node.sh"
